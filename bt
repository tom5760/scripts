#!/usr/bin/env fish
#
# Swap Bluetooth headset profiles
#
# Based a bit on:
#   https://askubuntu.com/a/1009156

function reset
  rfkill block bluetooth
  sleep 0.1
  rfkill unblock bluetooth
end

function speak
  echo 'Switching to HSP/HFP profile...'
  set_profile headset_head_unit
  sleep 0.1
  set_sink
  set_source
end

function listen
  echo 'Switching to A2DP profile...'
  set_profile a2dp_sink
  sleep 0.1
  set_sink
end

function toggle
  if pacmd list-cards | grep 'A2DP' | grep 'available: yes' 2>&1 > /dev/null
    speak

  else if pacmd list-cards | grep 'HSP/HFP' | grep 'available: yes' 2>&1 > /dev/null
    listen

  else
    echo 'No Bluetooth profiles found.'
    exit 1
  end
end

function get_card
  pacmd list-cards | grep 'bluez_card' | cut --characters=9- | head --bytes -2
end

function set_sink
  set --local sink (pacmd list-sinks | grep 'bluez_sink' | cut --characters=9- | head --bytes -2)
  pacmd set-default-sink "$sink"

  for i in (pacmd list-sink-inputs | grep 'index' | cut --characters=12-)
    pacmd move-sink-input "$i" "$sink"
  end
end

function set_source
  set --local source (pacmd list-sources | grep --only-matching 'bluez_source\..*\.headset_head_unit')
  pacmd set-default-source "$source"

  for i in (pacmd list-source-outputs | grep 'index' | cut --characters=12-)
    pacmd move-source-output "$i" "$source" 2> /dev/null
  end
end

function set_profile --argument-names profile
  pacmd set-card-profile (get_card) $profile 2> /dev/null
end

if test (count $argv) -lt 1
  toggle
else
  switch $argv[1]
    case reset
      reset

    case speak
      speak

    case listen
      listen

    case toggle '*'
      toggle
  end
end
